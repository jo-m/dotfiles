alias l "ls -luh"
alias k "ls -luh"
alias la "ls -lauh"

# Enable ... and .... etc
# See https://fishshell.com/docs/current/interactive.html#abbreviations
function multicd
    echo cd (string repeat -n (math (string length -- $argv[1]) - 1) ../)
end
abbr --add dotdot --regex '^\.\.+$' --function multicd

abbr --add objdump "objdump -Dslx --wide -M intel"

abbr --add py 'python3'

abbr -a exe chmod +x

# tmux: attach, 256 colors, fix unicode
alias tmx 'TERM=screen-256color-bce tmux -2u new-session -A -s main'
# open new session with same windows, e.g. for log view
alias tmx2 'TERM=screen-256color-bce tmux -2u new-session -t "main"'

function md -d "Create a directory and cd into it"
    command mkdir $argv
    if test $status = 0
        switch $argv[(count $argv)]
            case '-*'

            case '*'
                cd $argv[(count $argv)]
                return
        end
    end
end

function mvv -d "single arg mv with filename edit"
    if test (count $argv) != 1
        command mv $argv
        return $status
    end

    set mv_src "$argv[1]"
    set -g mv_dest "$argv[1]"
    vared mv_dest

    command mv -v -- "$mv_src" "$mv_dest"

    set -e mv_dest
end

function pdfmg -a outfile_pdf -d "merge all the PDF files in the current directory to a single one"
    # default arg
    set -q outfile_pdf[1]
    or set outfile_pdf "merged.pdf"

    # check ending
    if not string match -q -- "*.pdf" "$outfile_pdf"
        echo "'$outfile_pdf' does not have a .pdf ending!"
        return 1
    end

    # check if exists
    if test -f "$outfile_pdf"
        echo "'$outfile_pdf' already exists!"
        return 1
    end

    pdftk *.pdf cat output "$outfile_pdf"

    echo "Merged into '$outfile_pdf'."
end

function mem
    ps -eo rss,pid,euser,args:100 --sort %mem | awk '{printf $1/1024 "MB"; $1=""; print }'
end

# Gnome trash on CLI
alias trash "gio trash"

{{ if or (eq .chezmoi.osRelease.id "ubuntu") (eq .chezmoi.osRelease.id "pop") -}}
# APT aliases
alias ai "sudo apt install"
alias ai "sudo apt install"
alias ap "sudo apt purge"
alias ad "sudo apt update"
alias adu "sudo apt update && sudo apt dist-upgrade"
alias as "aptitude -F \"* %p -> %d \n(%v/%V)\" \
        --no-gui --disable-columns search" # search package
{{- end -}}
